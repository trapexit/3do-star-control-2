; generated by Norcroft  ARM C vsn 4.41 (Advanced RISC Machines) [Feb 17 1993]

INTERSECT_LEFT		EQU	(1 << 0)
INTERSECT_TOP		EQU	(1 << 1)
INTERSECT_RIGHT		EQU	(1 << 2)
INTERSECT_BOTTOM	EQU	(1 << 3)
INTERSECT_NOCLIP	EQU	0x80
CLEAR_NOCLIP		EQU	~0x80

        AREA |C$$code|, CODE, READONLY
|x$codeseg|

        EXPORT  BoxIntersect
BoxIntersect
        STMDB   sp!,{r4-r11,lr}
	
	LDMIA	a1,{r4-r7}	; pr1
	LDMIA	a2,{r8-r11}	; pr2
	
        MOV     a1,#INTERSECT_NOCLIP	; intersect_code (return)
	
	SUB	r4,r4,r8	; x1 = pr1->corner.x - pr2->corner.x
        SUB     a4,r10,r4	; delta = pr2->extent.width - x1
	
        CMP     a4,r6		; delta <= w1?
; delta < w1
	MOVLT	r6,a4		; w1 = delta
	MOVLT	a1,#0		; intersect_code = 0 (i.e., clear NOCLIP bit)
; delta <= w1
	ORRLE	a1,a1,#INTERSECT_RIGHT	; intersect_code |= INTERSECT_RIGHT
	
        CMP     r4,#0		; x1 <= 0?
; x1 < 0
        ADDLT	r6,r6,r4	; w1 += x1
	MOVLT	r4,#0		; x1 = 0
        ANDLT	a1,a1,#CLEAR_NOCLIP	; intersect_code &= ~INTERSECT_NOCLIP
; x1 <= 0
        ORRLE	a1,a1,#INTERSECT_LEFT	; intersect_code |= INTERSECT_LEFT
	
        CMP     r6,#0		; w1 > 0?
; w1 <= 0
	MOVLE	a1,#0
	LDMLEIA	sp!,{r4-r11,pc}	; return 0
		
	SUB	r5,r5,r9	; y1 = pr1->corner.y - pr2->corner.y
	SUB	a2,r11,r5	; delta = h2 - y1
	
	CMP	a2,r7		; delta <= h1?
; delta < h1
	MOVLT	r7,a2		; h1 = delta
	ANDLT	a1,a1,#CLEAR_NOCLIP	; intersect_code &= ~INTERSECT_NOCLIP
; delta <= h1
	ORRLE	a1,a1,#INTERSECT_BOTTOM	; intersect_code |= INTERSECT_BOTTOM

        CMP     r5,#0		; y1 <= 0?
; y1 < 0
	ADDLT	r7,r7,r5	; h1 += y1
	MOVLT	r5,#0		; y1 = 0
        ANDLT	a1,a1,#CLEAR_NOCLIP	; intersect_code &= ~INTERSECT_NOCLIP
; y1 <= 0
        ORRLE	a1,a1,#INTERSECT_TOP	; intersect_code |= ~INTERSECT_TOP
	
	CMP	r7,#0		; h1 > 0?
; h1 <= 0
	MOVLE	a1,#0
	LDMLEIA	sp!,{r4-r11,pc}	; return 0
		
	ADD	r4,r4,r8	; pinter->corner.x = x1 + pr2->corner.x
	ADD	r5,r5,r9	; pinter->corner.y = y1 + pr2->corner.y
	
	STMIA	a3,{r4-r7}
        LDMIA   sp!,{r4-r11,pc}	; return intersect_code
	

        EXPORT  BoxUnion
BoxUnion
        STMDB   sp!,{r4-r9,lr}
	
	LDMIA	a1,{r4-r7}
	LDMIA	a2,{a1,a4,r8-r9}
	
        SUBS    r4,r4,a1
        SUBMI   r8,r8,r4
        ADDMI   a1,a1,r4
        ADDPL   r6,r6,r4
	
        SUBS    r5,r5,a4
        SUBMI   r9,r9,r5
        ADDMI   a4,a4,r5
        ADDPL   r7,r7,r5
	
	SUBS	r6,r6,r8
	ADDGT	r8,r8,r6
	
	SUBS	r7,r7,r9
	ADDGT	r9,r9,r7
	
	STMIA	a3,{a1,a4,r8-r9}
	
        LDMIA   sp!,{r4-r9,pc}



        AREA |C$$data|,DATA

|x$dataseg|

        END
